allprojects {
    task wrapper(type: Wrapper) {
        gradleVersion = '2.14'
    }
}

subprojects {
    /*
     * Not applying the bintray plugin. Couldn't get it working properly when declared
     * in subprojects, and when declared at root level of this script, it caused an httpcomponents
     * classpath issue with ml-gradle in the core project. So for now, each project has to
     * duplicate bintray publishing configuration.
     */

    apply plugin: "java"
    apply plugin: "eclipse"
    apply plugin: "idea"
    apply plugin: "checkstyle"

    repositories {
        jcenter()
        maven { url "http://repo.spring.io/milestone" }
        maven { url "http://developer.marklogic.com/maven2/" }
        mavenLocal()
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    sourceSets.test.resources.srcDir 'src/test/java'

    checkstyle {
        configFile = file("${project.rootDir}/dev-tools/checkstyle/checkstyle.xml")
        toolVersion = '6.19'
    }
}

configure(subprojects.findAll {it.name != 'jobs' && it.name != 'configurations'}) {
    apply plugin: "maven-publish"

    task sourcesJar(type: Jar, dependsOn: classes) {
        baseName = project.name
        version = project.version
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    jar {
        baseName = project.name
        version = project.version
    }

    /*
     * Assumes each project has a single publication.
     */
    publishing {
        publications {
            mainJavaWithSources(MavenPublication) {
                groupId group
                artifactId project.name
                version project.version
                from components.java
                artifact sourcesJar
            }
        }
    }
}

/**
 * Include every project that you want to be part of the msb application.
 */
project(':jobs') {
    dependencies {
        runtime project(':marklogic-spring-batch-core')
        runtime project(':configurations:marklogic-spring-batch-file-importers')
        runtime project(':configurations:marklogic-spring-batch-shapefiles')
        runtime project(':configurations:marklogic-spring-batch-sql')
    }
}

project(':marklogic-spring-batch-test') {
    dependencies {
        compile project(':marklogic-spring-batch-core')
    }
}

configure(subprojects.findAll {it.name != 'marklogic-spring-batch-core' && it.name != "marklogic-spring-batch-test"}) {
    dependencies {
        compile project(':marklogic-spring-batch-core')
        testCompile project(':marklogic-spring-batch-test')
    }
}
