plugins {
    id "com.jfrog.bintray" version "1.6"
    id "maven-publish"
    id "application"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'lib')
    compile "com.marklogic:marklogic-spring-batch:0.4.0"
    testCompile "com.marklogic:ml-junit:2.6.0"
    testCompile "org.springframework.batch:spring-batch-test:3.+"

    // For testing SQL readers with an embedded database
    testCompile "org.hsqldb:hsqldb:2.3.3"

    // For ReadUsersFromHsqlUsingUserWriterTest
    testCompile "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.6.3"
    testRuntime "org.codehaus.woodstox:woodstox-core-asl:4.4.1"
}

sourceSets.test.resources.srcDir 'src/test/java'

mainClassName = "com.marklogic.spring.batch.Main"

task testJar(type: Jar, dependsOn: testClasses) {
    classifier = "test"
    includes = ["com/marklogic/spring/batch/JobTestConfig.java",
                "com/marklogic/spring/batch/SpringBatchNamespaceProvider.java",
                "com/marklogic/spring/batch/test/",
                "com/marklogic/spring/batch/job/AbstractJobTest.java"]
    from sourceSets.test.allJava
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        jobs(MavenPublication) {
            groupId group
            artifactId jobsArtifact
            version jobsVersion
            from components.java
        }
        sourcesJava(MavenPublication) {
            groupId group
            artifactId jobsArtifact
            version jobsVersion
            artifact sourcesJar
        }
        testJava(MavenPublication) {
            groupId group
            artifactId jobsArtifact
            version jobsVersion
            artifact testJar
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['jobs','sourcesJava', 'testJava']
    pkg {
        repo = 'maven'
        name = jobsArtifact
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/sastafford/marklogic-spring-batch.git'
        version {
            name = jobsVersion
            released = new Date()
            vcsTag = jobsVersion
        }
    }
}